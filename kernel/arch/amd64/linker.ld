OUTPUT_FORMAT(elf64-x86-64)
OUTPUT_ARCH(i386:x86-64)
ENTRY(_start)

KERNEL_VMA = 0xFFFFFFFF80000000;

SECTIONS
{
    /* place the image at the chosen virtual address */
    . = KERNEL_VMA;

    .text : ALIGN(0x1000)
    {
        KEEP(*(.db_request))
        *(.text.entry)
        *(.text .text.*)
    }

    .rodata : ALIGN(0x1000)
    {
        *(.rodata .rodata.*)
    }

    .driver_init : ALIGN(0x8)
    {
        __driver_init_start = .;
        KEEP(*(SORT(.driver_init*)))
        __driver_init_end = .;
    }

    .data : ALIGN(0x1000)
    {
        *(.data .data.*)
    }

    .bss : ALIGN(0x1000)
    {
        __bss_start = .;
        *(.bss .bss.*)
        *(COMMON)
        __bss_end = .;
    }

    /* final kernel end symbol */
    PROVIDE(__kernel_end = .);

    /DISCARD/ :
    {
        *(.comment)
        *(.note*)
        *(.eh_frame*)
    }
}
